PREFIX wp:    <http://vocabularies.wikipathways.org/wp#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

SELECT DISTINCT
	?pname
	?pw
	?displayNamePw
	?interaction
	?intType
	?partPred
	?participant
	?ctrlRxn
	?ctrlRxnType
	?ctrlRxnDir
	?ctrlEntity
	?ctrlEntityType
	?ctrlEntityLocRef
	(GROUP_CONCAT(DISTINCT ?intPubTitle;SEPARATOR=",") AS ?intPubTitles)
	(GROUP_CONCAT(DISTINCT ?intPubDb;SEPARATOR=",") AS ?intPubDbs)
	(GROUP_CONCAT(DISTINCT ?intPubId;SEPARATOR=",") AS ?intPubIds)

#FROM <http://www.ontotext.com/explicit>

WHERE {
	VALUES ?pname {"wnt_noncanonical_pathway"^^xsd:string "wnt_signaling_pathway"^^xsd:string}
#    VALUES ?pw {<http://pathwaycommons.org/pc12/Pathway_cb80f0022b93ffbc95f93ebca756170b> <http://pathwaycommons.org/pc12/Pathway_3689db7d75c82d3b603ace8a36bf5a0c>}
#	VALUES ?pw {<http://pathwaycommons.org/pc12/Pathway_e7040e19fdf8b91f7b53109d2513ca6f> <http://pathwaycommons.org/pc12/Pathway_af908790b4b115e69879e7bf41c80a09>}
	VALUES ?pw {{{pathVals}}}


    ?pw a bp:Pathway ;
            bp:name ?pname ;
            bp:displayName ?displayNamePw ;
            bp:pathwayComponent* ?interaction .
    ?intType ^a ?interaction ;
              rdfs:subClassOf+ bp:Interaction .
    ?interaction ?partPred ?participant ;
                 bp:xref [ a bp:PublicationXref ;
                           bp:title ?intPubTitle ;
                           bp:id ?intPubId ;
                           bp:db ?intPubDb] .
    ?partPred rdfs:subPropertyOf bp:participant .

    OPTIONAL {
        ?interaction ^bp:controlled ?ctrlRxn .
        ?ctrlRxn a ?ctrlRxnType .
        ?ctrlRxnType rdfs:subClassOf bp:Control .
        OPTIONAL {
            ?ctrlRxn bp:controlType ?ctrlRxnDir ;
                     bp:controller ?ctrlEntity .
            ?ctrlEntity a ?ctrlEntityType .
            OPTIONAL {
                       ?ctrlEntity bp:cellularLocation [ bp:xref ?ctrlEntityLocRef ] .
            }
        }

    }
#    FILTER(BOUND(?ctrlRxn))
}

GROUP BY ?pname
         ?pw
				 ?displayNamePw
				 ?interaction
				 ?intType
				 ?partPred
				 ?participant
				 ?ctrlRxn
				 ?ctrlRxnType
				 ?ctrlRxnDir
				 ?ctrlEntity
				 ?ctrlEntityType
				 ?ctrlEntityLocRef
ORDER BY ?pw ?participant ?interaction
LIMIT {{{resultLim}}}
OFFSET {{{resultOffset}}}
