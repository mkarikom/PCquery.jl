PREFIX wp:    <http://vocabularies.wikipathways.org/wp#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

SELECT DISTINCT
	?pname
	?pw
	?displayNamePw
	?interaction
	?intDisplayName
	?intType
	?partPred
	?participant
	?participantType
	?ctrlRxn
	?ctrlRxnDisplayName
	?ctrlRxnType
	?ctrlRxnDir
	?ctrlEntity
	?ctrlEntityType
	?ctrlEntityLocRef
	(GROUP_CONCAT(DISTINCT ?intPubTitle;SEPARATOR=",") AS ?intPubTitles)
	(GROUP_CONCAT(DISTINCT ?intPubDb;SEPARATOR=",") AS ?intPubDbs)
	(GROUP_CONCAT(DISTINCT ?intPubId;SEPARATOR=",") AS ?intPubIds)

#
{{{fromgraph}}}

WHERE {
#VALUES ?pw {<http://identifiers.org/reactome/R-HSA-2122948> <http://identifiers.org/reactome/R-HSA-2122947>}
#VALUES ?pw {<http://pathwaycommons.org/pc12/Pathway_b63e3e30297acb0ab2fec4250c1eb294> <http://pathwaycommons.org/pc12/Pathway_a655fac45ae26a6f8ab1542352531b8c>}

	VALUES ?pw {{{pathVals}}}

	?pw a bp:Pathway ;
					bp:pathwayComponent* ?interaction .
	?partPred rdfs:subPropertyOf bp:participant .
	?intType rdfs:subClassOf+ bp:Interaction .
	?interaction ?partPred ?participant ;
							 a ?intType ;
							 bp:xref [ a bp:PublicationXref ;
												 bp:title ?intPubTitle ;
												 bp:id ?intPubId ;
												 bp:db ?intPubDb] .
	?participant a ?participantType .
		OPTIONAL
		{
		 ?interaction bp:displayName ?intDisplayName .
		}
 		OPTIONAL
		{
			?pw bp:name ?pname ;
						bp:displayName ?displayNamePw .
		}

    OPTIONAL {
        ?interaction ^bp:controlled ?ctrlRxn .
        ?ctrlRxn a ?ctrlRxnType .
#        ?ctrlRxnType rdfs:subClassOf bp:Control .
				OPTIONAL
				{
				 ?ctrlRxn bp:displayName ?ctrlRxnDisplayName .
				}
        OPTIONAL {
            ?ctrlRxn bp:controlType ?ctrlRxnDir ;
                     bp:controller ?ctrlEntity .
            ?ctrlEntity a ?ctrlEntityType .
            OPTIONAL {
                       ?ctrlEntity bp:cellularLocation [ bp:xref ?ctrlEntityLocRef ] .
            }
        }

    }
#    FILTER(BOUND(?ctrlRxn))
}

GROUP BY ?pname
         ?pw
				 ?displayNamePw
				 ?interaction
				 ?intDisplayName
				 ?intType
				 ?partPred
				 ?participant
				 ?participantType
				 ?ctrlRxn
				 ?ctrlRxnDisplayName
				 ?ctrlRxnType
				 ?ctrlRxnDir
				 ?ctrlEntity
				 ?ctrlEntityType
				 ?ctrlEntityLocRef
ORDER BY ?pw ?participant ?interaction
LIMIT {{{resultLim}}}
OFFSET {{{resultOffset}}}
