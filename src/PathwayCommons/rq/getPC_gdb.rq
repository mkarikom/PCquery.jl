PREFIX wp:    <http://vocabularies.wikipathways.org/wp#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

SELECT DISTINCT
	?pw
	?displayNamePw
	?interaction
	?intDisplayName
	?intType
	?partPred
	?participant
	?participantType
	?ctrlRxn
	?ctrlRxnDisplayName
	?ctrlRxnType
	?ctrlRxnDir
	?ctrlEntity
	?ctrlEntityType
	?ctrlEntityLocRef


{{{fromgraph}}}

WHERE {

	VALUES ?pw {{{pathVals}}}
	VALUES ?intType {bp:Catalysis
											 bp:Modulation
											 bp:TemplateReaction
											 bp:TemplateReactionRegulation
											 bp:BiochemicalReaction
										   bp:ComplexAssembly
										   bp:Degradation
										   bp:Transport
										   bp:TransportWithBiochemicalReaction}
	?pw a bp:Pathway ;
					bp:pathwayComponent* ?interaction .
	?partPred rdfs:subPropertyOf* bp:participant .
	?intType rdfs:subClassOf+ bp:Interaction .
	FILTER NOT EXISTS { ?intType rdfs:subClassOf+ bp:Control }
	?interaction ?partPred ?participant ;
							 a ?intType .
	?participant a ?participantType .
	OPTIONAL
	{
		?interaction bp:xref [ a bp:PublicationXref ;
		bp:title ?intPubTitle ;
		bp:id ?intPubId ;
		bp:db ?intPubDb] .
	}

		OPTIONAL
		{
		 ?interaction bp:displayName ?intDisplayName .
		}
 		OPTIONAL
		{
			?pw bp:displayName ?displayNamePw .
		}

    OPTIONAL {
        ?interaction ^bp:controlled ?ctrlRxn .
        ?ctrlRxn a ?ctrlRxnType .
#        ?ctrlRxnType rdfs:subClassOf bp:Control .
				OPTIONAL
				{
				 ?ctrlRxn bp:displayName ?ctrlRxnDisplayName .
				}
        OPTIONAL {
            ?ctrlRxn bp:controlType ?ctrlRxnDir ;
                     bp:controller ?ctrlEntity .
            ?ctrlEntity a ?ctrlEntityType .
            OPTIONAL {
                       ?ctrlEntity bp:cellularLocation [ bp:xref ?ctrlEntityLocRef ] .
            }
        }

    }
#    FILTER(BOUND(?ctrlRxn))
}

GROUP BY ?pw
				 ?displayNamePw
				 ?interaction
				 ?intDisplayName
				 ?intType
				 ?partPred
				 ?participant
				 ?participantType
				 ?ctrlRxn
				 ?ctrlRxnDisplayName
				 ?ctrlRxnType
				 ?ctrlRxnDir
				 ?ctrlEntity
				 ?ctrlEntityType
				 ?ctrlEntityLocRef
ORDER BY ?pw ?participant ?interaction
LIMIT {{{resultLim}}}
OFFSET {{{resultOffset}}}
